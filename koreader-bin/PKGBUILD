# Maintainer: eNV25 <env252525@gmail.com>
# Upstream Maintainer: Mart√≠n Fdez <paziusss@gmail.com>

pkgname=koreader-bin
pkgver=2024.04
_fonts_commit=046976988aa33639d60d6ffd25c7a0ff50b72ac0
pkgrel=1
pkgdesc="Ebook reader application supporting PDF, DjVu, EPUB, FB2 and many more formats"
arch=(x86_64 aarch64 armv7h)
url="https://koreader.rocks"
license=('AGPL-3.0-or-later')
depends=(sdl2 noto-fonts ttf-droid)
provides=(koreader)
conflicts=(koreader)
source=("https://github.com/koreader/koreader-fonts/raw/$_fonts_commit/noto/NotoSansArabicUI-"{Regular,Bold}".ttf")
source_x86_64=("https://github.com/koreader/koreader/releases/download/v$pkgver/koreader-$pkgver-amd64.deb")
source_aarch64=("https://github.com/koreader/koreader/releases/download/v$pkgver/koreader-$pkgver-arm64.deb")
source_armv7h=("https://github.com/koreader/koreader/releases/download/v$pkgver/koreader-$pkgver-armhf.deb")

package() {
	bsdtar xf data.tar.* -C "$pkgdir"

	# HACK: koreader uses symlinks to fonts installed on Debian. Arch Linux uses a different directory.
	for font in "$pkgdir/usr/lib/koreader/fonts/droid/"*.ttf; do
		ln -sf ../../../../share/fonts/droid/"$(basename "$font")" "$font"
	done
	for font in "$pkgdir/usr/lib/koreader/fonts/noto/"*.ttf; do
		ln -sf ../../../../share/fonts/noto/"$(basename "$font")" "$font"
	done

	# HACK: NotoSansArabicUI isn't distributed by notofonts project anymore and is probably deprecated.
	#       But it is still hard-coded in koreader. We get it from GitHub.
	install -Dm644 "NotoSansArabicUI-"{Regular,Bold}".ttf" "$pkgdir/usr/lib/koreader/fonts/noto/"
}

sha256sums=('409e307217626e94a5ad3eb71b86ba19a4ef096c2e3699083a5e6b008a5ae9a3'
            '905ffee4bbe954d3fd725ed4f2c161dd2f6d2e304d2c725c1e0b8022194de3b9')
sha256sums_x86_64=('86a25164366accf34b2bff016fe39acbbd09a8a00c281d04a5b6f37aee67a4bc')
sha256sums_aarch64=('170c01f6c94a3a2610ddeb9ddad8aaa2feb218dc662227087ba9c5a6c1f6f159')
sha256sums_armv7h=('2e079641c1e6e50abfecc52e89d8e2972d16b0e15901619678ee768cd1d628cc')
