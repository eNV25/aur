# Maintainer: Kasei Wang <kasei@kasei.im>
# Contributor: eNV25 <env252525@gmail.com>

pkgname=quiche
pkgver=0.12.0
pkgrel=1
pkgdesc='an implementation of the QUIC transport protocol and HTTP/3 as specified by the IETF'
arch=('x86_64')
url='https://github.com/cloudflare/quiche'
license=('custom:BSD')
depends=('gcc-libs')
makedepends=('cmake' 'git' 'go' 'cargo')
options=(!lto)
source=(
  "git+https://github.com/cloudflare/quiche?tag=$pkgver"
  "git+https://github.com/google/boringssl?commit=f1c75347daa2ea81a941e953f2263e0a4d970c8d" # commit shown in github
)

prepare() {
  cd "$srcdir/$pkgname"
  git submodule init
  git config submodule.boringssl.url "$srcdir/boringssl"
  git submodule update
  cargo fetch --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$srcdir/$pkgname"
  cargo build --package quiche --frozen --release --features ffi,pkg-config-meta,qlog
  sed '
    /includedir=.*/s||includedir=/usr/include|
    /libdir=.*/s||libdir=/usr/lib|
  ' -i target/release/quiche.pc
}

check() {
  cd "$srcdir/$pkgname"
  cargo test --frozen
}

package() {
  cd "$srcdir/$pkgname"

  install -Dm644 quiche/include/quiche.h -t "$pkgdir/usr/include/"
  install -Dm644 target/release/libquiche.a -t "$pkgdir/usr/lib/"
  install -Dm755 target/release/libquiche.so -t "$pkgdir/usr/lib/"
  install -Dm644 target/release/quiche.pc -t "$pkgdir/usr/lib/pkgconfig/"

  install -Dm644 COPYING -t "$pkgdir/usr/share/licenses/quiche/"
}

sha256sums=('SKIP'
            'SKIP')
