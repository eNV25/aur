# Maintainer: eNV25 <env252525@gmail.com>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Lucien Immink <l.immink@student.fnt.hvu.nl>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=curl-nghttp3
_pkgname=curl
pkgver=7.81.0
pkgrel=2
pkgdesc='An URL retrieval utility and library -- built with nghttp3, ngtcp2 and gnutls for HTTP/3 support'
arch=('x86_64')
url='https://curl.haxx.se'
license=('MIT')
options=('debug')
depends=('ca-certificates' 'brotli' 'libbrotlidec.so' 'krb5' 'libgssapi_krb5.so'
         'libidn2' 'libidn2.so' 'libnghttp2' 'libpsl' 'libpsl.so' 'libssh2' 'libssh2.so'
         'gnutls' 'libnghttp3' 'libnghttp3.so' 'libngtcp2' 'libngtcp2.so'
         'zlib' 'zstd' 'libzstd.so')
source=("https://curl.haxx.se/download/${_pkgname}-${pkgver}.tar.gz"{,.asc})
sha512sums=('e3084f0fa083f7f93eac923edbfdddb5fd0a372b94673ba9d4427a2b95508898c15ecdf63b99a1c1f6cf3215e27b06cbaa2b7073df038d43b362e586f92495d3'
            'SKIP')
validpgpkeys=('27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2') # Daniel Stenberg

build() {
  mkdir -p build
  cd build

  "${srcdir}/${_pkgname}-${pkgver}"/configure \
    \
    --without-openssl \
    --with-gnutls \
    --with-nghttp3 \
    --with-ngtcp2 \
    --disable-shared \
    \
    --prefix='/usr' \
    --mandir='/usr/share/man' \
    --disable-ldap \
    --disable-ldaps \
    --disable-manual \
    --enable-ipv6 \
    --enable-versioned-symbols \
    --enable-threaded-resolver \
    --with-gssapi \
    --with-libssh2 \
    --with-random='/dev/urandom' \
    --with-ca-bundle='/etc/ssl/certs/ca-certificates.crt'

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd build

  make DESTDIR="${pkgdir}" install

  cd "${srcdir}/${_pkgname}-${pkgver}"

  # license
  install -Dt "${pkgdir}/usr/share/licenses/$_pkgname" -m0644 COPYING

  # fixup for no conflict with core/curl
  cd "$pkgdir"

  mv usr/bin/curl usr/bin/curl3
  rm -rf usr/bin/curl-config

  rm -rf usr/{include,share,lib}
}
