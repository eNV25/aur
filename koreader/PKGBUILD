# Maintainer: eNV25 <env252525@gmail.com>
# Upstream Maintainer: Mart√≠n Fdez <paziusss@gmail.com>

pkgname=koreader-bin
pkgver=2024.03.1
pkgrel=1
pkgdesc="Ebook reader application supporting PDF, DjVu, EPUB, FB2 and many more formats"
arch=(x86_64 aarch64 armv7h)
url="https://koreader.rocks"
license=('AGPL-3.0-or-later')
depends=(sdl2 noto-fonts ttf-droid)
provides=(koreader)
conflicts=(koreader)
source=("https://salsa.debian.org/fonts-team/fonts-noto/-/raw/debian/20201225-2/hinted/ttf/NotoSansArabicUI/NotoSansArabicUI-"{Regular,Bold}".ttf")
source_x86_64=("https://github.com/koreader/koreader/releases/download/v$pkgver/koreader-$pkgver-amd64.deb")
source_aarch64=("https://github.com/koreader/koreader/releases/download/v$pkgver/koreader-$pkgver-arm64.deb")
source_armv7h=("https://github.com/koreader/koreader/releases/download/v$pkgver/koreader-$pkgver-armhf.deb")

package() {
	bsdtar xf data.tar.* -C "$pkgdir"

	# HACK: koreader uses symlinks to fonts installed on Debian. Arch Linux uses a different directory.
	for font in "$pkgdir/usr/lib/koreader/fonts/droid/"*.ttf; do
		ln -sf ../../../../share/fonts/droid/"$(basename "$font")" "$font"
	done
	for font in "$pkgdir/usr/lib/koreader/fonts/noto/"*.ttf; do
		ln -sf ../../../../share/fonts/noto/"$(basename "$font")" "$font"
	done

	# HACK: NotoSansArabicUI isn't distributed by notofonts project anymore and is probably deprecated.
	# 			But it is still hard-coded in koreader.
	install -Dm644 "NotoSansArabicUI-"{Regular,Bold}".ttf" "$pkgdir/usr/lib/koreader/fonts/noto/"
}

sha256sums=('43a90e0f0e9ca712ff0588dc2b119823d569367a6d0061eb5f7bfb9bfbe8cecc'
            'ef5d9ce3d5b20a83bdc156ef48edc1e159322ada95ebacc133476c92f0bd102a')
sha256sums_x86_64=('119de2aa9d00d81670237e379efbe9ef5446431e853deb14072e0b85ae18cf85')
sha256sums_aarch64=('f41bbe9967c9b8f687e6757bd4932b7068a989959f71fd7d29f55ab825bddbe2')
sha256sums_armv7h=('861b0c35f6628c7d24700ade66a18a4f05e2bae00159d4d1dd3af19163c7eca1')
