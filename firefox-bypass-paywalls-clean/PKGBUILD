# Maintainer: eNV25 <env252525@gmail.com>

pkgname=firefox-bypass-paywalls-clean
_pkgname=bypass-paywalls-firefox-clean
pkgver=3.0.4.0
pkgrel=1
pkgdesc="Read articles from (supported) sites that implement a paywall."
arch=(any)
url="https://gitlab.com/magnolia1234/bypass-paywalls-firefox-clean"
license=('MIT')
groups=(firefox-addons)
makedepends=(jq web-ext)
source=("https://gitlab.com/magnolia1234/$_pkgname/-/archive/v3.0.4.0/$_pkgname-v$pkgver.tar.gz")
sha256sums=('f3bbd4dddc6eee4949266e0ce24df2b09edcebeb5fe748ab2995059c12679c20')

prepare() {
	cd "$_pkgname-v$pkgver"
	# Remove what looks like analytics
	# Remove update_url since this is managed by pacman.
	printf '%s' "$(jq '
		.background.scripts |= map(select(. != "bpc_count_daily_users.js")) |
		del(.browser_specific_settings.gecko.update_url)
        ' manifest.json)" \
		>manifest.json
	# Save id for later use
	jq -r .browser_specific_settings.gecko.id manifest.json \
		>id
}

build() {
	cd "$_pkgname-v$pkgver"
	web-ext build -a . -s .
}

package() {
	cd "$_pkgname-v$pkgver"
	# Install for firefox
	install -v -Dm644 "bypass_paywalls_clean-$pkgver.zip" "$pkgdir/usr/lib/firefox/browser/extensions/$(<id).xpi"
	# Do it manually
	## Install for the wierdos out there
	#for _b in librewolf firedragon; do
	#        ln -v -sf "$pkgdir/usr/lib/firefox/browser/extensions/$(<id).xpi" "$pkgdir/usr/lib/$_b/browser/extensions/$(<id).xpi"
	#done
	# Install license
	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
