# Maintainer: eNV25 <env252525@gmail.com>

pkgname=void-tools
_pkgname=void-runit
pkgver=20220329
pkgrel=1
pkgdesc="Some not Void specific tools from void-runit"
arch=(x86_64)
url="https://github.com/void-linux/$_pkgname"
license=('custom: public domain' 'custom: CC0')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")

build() {
	cd "$_pkgname-$pkgver"
	make
}

package() {
	cd "$_pkgname-$pkgver"
	make DESTDIR="$pkgdir" PREFIX=/usr install
	cd "${pkgdir}"
	rm -vrf etc/
	rm -vrf usr/lib
	mv -v usr/sbin usr/bin
	for _cmd in halt poweroff reboot shutdown vlogger; do
		rm -vf usr/bin/$_cmd usr/share/man/man*/$_cmd.*
	done
}

sha256sums=('43037b1cbf177a3ee6d690cc652eedf819ca37f44636311b55aa716e159a4718')
