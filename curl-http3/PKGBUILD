# Maintainer: Hendrik 'T4cC0re' Meyer <aur@t4cc0.re>

pkgname=curl-http3
pkgver=7.81.0
_quiche_ref=0.12.0
_bssl_ref=f1c75347daa2ea81a941e953f2263e0a4d970c8d
pkgrel=1
pkgdesc="An URL retrieval utility and library - compiled with HTTP/3 support - binary is called curl3"
arch=('x86_64')
url="https://curl.haxx.se"
license=('MIT')
depends=('ca-certificates' 'openssl' 'zlib' 'libpsl' 'libnghttp2'
  'libidn2' 'libidn2.so')
makedepends=('rust' 'git' 'go' 'cmake')
options=(!lto)                                            # needed for quiche
validpgpkeys=('27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2') # Daniel Stenberg
source=("https://curl.haxx.se/download/curl-$pkgver.tar.gz"{,.asc}
        "quiche-$_quiche_ref.tar.gz::https://github.com/cloudflare/quiche/archive/$_quiche_ref.tar.gz"
        "boringssl-$_bssl_ref.tar.gz::https://github.com/google/boringssl/archive/$_bssl_ref.tar.gz") # commit shown in quiche github
#source=("curl-$pkgver.zip::https://github.com/curl/curl/archive/master.zip")

prepare() {
  ln -srf "$srcdir/quiche-$_quiche_ref" "$srcdir/quiche"
  ln -srf "$srcdir/boringssl-$_bssl_ref" "$srcdir/boringssl"
  cd "$srcdir/quiche"
  rm -rf quiche/deps/boringssl
  ln -srf "$srcdir/boringssl" quiche/deps/boringssl
  cargo fetch --target "$CARCH-unknown-linux-gnu"
}

build() {
  mkdir build
  cd build

  cargo build \
    --manifest-path "$srcdir/quiche/Cargo.toml" --target-dir "$PWD/target" \
    --package quiche --frozen --release --features ffi,pkg-config-meta,qlog
  rm target/release/libquiche.so # use libquiche.a
  mkdir "$srcdir/boringssl/src/lib"
  ln -vnf $(find target/release -name libcrypto.a -o -name libssl.a) "$srcdir/boringssl/src/lib/"

  ## Arch Linux build flags + BoringSSL and quiche
  "$srcdir/curl-$pkgver/configure" \
    LDFLAGS="-Wl,-rpath,$PWD/target/release $LDFLAGS" \
    --with-openssl="$srcdir/boringssl/src" \
    --with-quiche="$PWD/target/release" \
    --without-gnutls \
    --enable-alt-svc \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --disable-ldap \
    --disable-ldaps \
    --disable-manual \
    --disable-shared \
    --enable-ipv6 \
    --enable-threaded-resolver \
    --with-gssapi \
    --with-libssh2 \
    --with-random='/dev/urandom' \
    --with-ca-bundle='/etc/ssl/certs/ca-certificates.crt'
  make
}

package() {
  cd build

  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" install -C scripts

  cd "$srcdir/curl-$pkgver"
  # license
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 COPYING
  cd ..

  # rename curl to avoid conflicts
  mv "${pkgdir}/usr/bin/curl" "${pkgdir}/usr/bin/curl3"

  # Remove stuff that would conflict with core/curl
  rm -rf "${pkgdir}/usr/lib" "${pkgdir}/usr/share" "${pkgdir}/usr/include" "${pkgdir}/usr/bin/curl-config"
}
sha512sums=(e3084f0fa083f7f93eac923edbfdddb5fd0a372b94673ba9d4427a2b95508898c15ecdf63b99a1c1f6cf3215e27b06cbaa2b7073df038d43b362e586f92495d3             SKIP             723960b439fc6d880da88500713875fad56c7076102ca67631611d8c9529a7e82598fec42a617133c379686b4e05c8c6fc00ff10104b92e0d2f45f5f6912be30             016cb11767e5792335cd8794087e92c5a1d76767ab7042afcf2eb9b568c53c7b154c34fe3b9be8c042a3034ebfc1d347cb897b4eded3ae3a1ec11c2f65e9ae07)
